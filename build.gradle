buildscript {
  repositories { 
    jcenter() 
  }
  dependencies {
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.3'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
  }
}

apply plugin: 'idea'
apply plugin: 'artifactory'
apply plugin: 'com.jfrog.bintray'

ext {
  ratpackVersion = "1.1.0"
}

allprojects {
  group = "com.danveloper.ratpack.graph"

  apply plugin: 'groovy'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'artifactory'
  apply plugin: 'com.jfrog.bintray'

  repositories {
    jcenter()
  }

  dependencies {
    compile "io.ratpack:ratpack-core:${ratpackVersion}"
    compile "io.ratpack:ratpack-guice:${ratpackVersion}"
    testCompile "org.codehaus.groovy:groovy-all:2.4.3"
    testCompile "io.ratpack:ratpack-groovy-test:${ratpackVersion}"
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
 }

  artifactory {
    contextUrl = 'http://oss.jfrog.org'
  }

  artifactoryPublish { task ->
    rootProject.artifactory {
      publish {
        repository {
          repoKey = version.endsWith("-SNAPSHOT") ? 'oss-snapshot-local' : 'oss-release-local'
          gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.hasTask(task)) {
              username = bintrayUser
              password = bintrayKey
            }
          }
        }
      }
    }
  }

  bintray {
    user = bintrayUser
    key = bintrayKey
    pkg.repo = "maven"
    pkg.name = "ratpack-graph"
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
          classifier "sources"
        }
      }

    }
  }
}
